group 'io.thelandscape'
version '0.4.1'

buildscript {
    ext.kotlin_version = '1.1.4'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
    }
}

allprojects {

    apply plugin: "kotlin"
    apply plugin: "java"
    apply plugin: "maven"

    kotlin { experimental { coroutines 'enable' } }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }
}

project(":") {
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.18'

        compile "org.apache.httpcomponents:httpclient:4.5.2"
        compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.4'

        compile 'org.jsoup:jsoup:1.10.2'

        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.1'

        compile "com.github.andrewoma.kwery:core:0.15"
        compile "com.github.andrewoma.kwery:fetcher:0.15"
        compile "com.github.andrewoma.kwery:mapper:0.15"

        compile group: 'com.zaxxer', name: 'HikariCP', version: '2.6.3'

        compile group: 'com.google.guava', name: 'guava', version: '19.0'

        testCompile "com.nhaarman:mockito-kotlin:1.4.0"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
        testCompile "junit:junit:4.12"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(":example") {
    apply plugin: "kotlin"
    apply plugin: "com.github.johnrengelman.shadow"

    jar {
        manifest {
            attributes(
                    "Manifest-Version": "1.0",
                    "Created-By": "@brianmadden",
                    "Main-Class": "MainKt")
        }
    }

    dependencies {
        compile project(":")
    }
}